function(format_snippet LIST GLUE PRE POST)
    set(LIST ${${LIST}})
    set(COLUMN_LIMIT 120)
    set(TAB_LENGTH 4)
    string(REPEAT " " ${TAB_LENGTH} TAB)
    string(LENGTH ${PRE} INDENTION_LENGTH)
    math(EXPR INDENTION_LENGTH "${TAB_LENGTH} + ${INDENTION_LENGTH}")
    string(REPEAT " " ${INDENTION_LENGTH} INDENTION)
    list(LENGTH LIST REMAINING)
    set(LOOP 0)
    string(LENGTH ${GLUE} GLUE_LENGTH)
    string(APPEND OUTPUT ${TAB})
    string(APPEND OUTPUT ${PRE})
    while(REMAINING GREATER 0)
        string(LENGTH ${OUTPUT} OUTPUT_LENGTH)
        list(GET LIST 0 APPENDENT)
        set(APPENDENT "${ARGV4}${APPENDENT}${ARGV5}")
        string(LENGTH ${APPENDENT} APPENDENT_LENGTH)
        math(EXPR POTENTIAL_LENGTH "${OUTPUT_LENGTH} + ${APPENDENT_LENGTH} + ${GLUE_LENGTH}")
        if(POTENTIAL_LENGTH GREATER ${COLUMN_LIMIT} AND OUTPUT STREQUAL ${INDENTION})
            message(FATAL_ERROR "Cannot fit within column limit")
        endif()
        if(POTENTIAL_LENGTH LESS_EQUAL ${COLUMN_LIMIT})
            string(APPEND OUTPUT ${APPENDENT})
            if(REMAINING GREATER 1)
                string(APPEND OUTPUT ${GLUE})
            endif()
            list(REMOVE_AT LIST 0)
            list(LENGTH LIST REMAINING)
        else()
            list(APPEND OUTPUT_LIST ${OUTPUT})
            set(OUTPUT ${INDENTION})
        endif()
    endwhile()
    if(OUTPUT_LIST)
        list(APPEND OUTPUT_LIST ${OUTPUT})
        list(JOIN OUTPUT_LIST "\n" OUTPUT)
    endif()
    string(APPEND OUTPUT "${POST}\n")
    set(FORMATTED_SNIPPET ${OUTPUT})
    return(PROPAGATE FORMATTED_SNIPPET)
endfunction()
